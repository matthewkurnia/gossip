WHITESPACE = _{ " " }
COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

identifier           = ${ (CASED_LETTER | " " | NUMBER)+ }
text_allowed_symbols =  { "(" | ")" | ":" | "." | "," | "?" | "!" | "&" | "$" | "/" | "-" | "+" | "*" | "'" | "\"" | "%" }
text                 = ${ (CASED_LETTER | " " | text_allowed_symbols | NUMBER)+ }

bold_text   = ${ "@bold[" ~ text ~ "]" }
italic_text = ${ "@italic[" ~ text ~ "]" }
wave_text   = ${ "@wave[" ~ text ~ "]" }
shake_text  = ${ "@shake[" ~ text ~ "]" }
styled_text = ${ (bold_text | italic_text | wave_text | shake_text | text)+ }

regular_line = { identifier ~ ":" ~ styled_text }

choice      = { "?>" ~ styled_text ~ "=>" ~ identifier }
choice_line = { identifier ~ ":" ~ NEWLINE ~ choice ~ (NEWLINE+ ~ choice)* }

variable_identifier = { XID_CONTINUE+ }
enum_value          = { variable_identifier }
int_value           = { NUMBER+ }

set_enum = { variable_identifier ~ "=" ~ enum_value }
set_int  = { variable_identifier ~ "=" ~ int_value }
inc_int  = { variable_identifier ~ "+=" ~ int_value }
dec_int  = { variable_identifier ~ "-=" ~ int_value }
set_line = { set_int | set_enum | inc_int | dec_int }

event_line = { "event" ~ variable_identifier }

line      = { choice_line | regular_line | set_line | event_line }
fragment  = { "#>" ~ identifier ~ (NEWLINE+ ~ line)+ }
fragments = { SOI ~ NEWLINE* ~ fragment? ~ (NEWLINE+ ~ fragment)* ~ NEWLINE* ~ EOI }
